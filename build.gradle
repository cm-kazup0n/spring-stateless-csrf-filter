group 'jp.classmethod'
version '1.0-SNAPSHOT'

def springVersion = "4.2.4.RELEASE"
def springBoot1Version = "1.3.2.RELEASE"
def springBoot2Version = "2.0.2.RELEASE"
def thymeLeaf2Version = "2.1.4.RELEASE"
def thymeLeaf3Version = "3.0.9.RELEASE"
def servletAPIVersion = "3.1.0"


buildscript {
    repositories {
        mavenCentral()
        // The next repo is only needed while using SNAPSHOT versions
        maven {  url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven {  url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.5.4"
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.3"
    }
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: 'com.github.spotbugs'
    apply plugin: "jacoco"


    sourceCompatibility = 1.8


    repositories {
        jcenter()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.3'
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    configurations {
        providedCompile
    }

    sourceSets {
        main.compileClasspath += configurations.providedCompile
        test.compileClasspath += configurations.providedCompile
        test.runtimeClasspath += configurations.providedCompile
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination(new File("${buildDir}/reports/jacoco/report.xml"))
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}

project(':stateless-csrf-filter-core') {
    dependencies {
        compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
        compile group: 'org.springframework', name: 'spring-webmvc', version: "${springVersion}"
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        providedCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
        providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: "${servletAPIVersion}"
        testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "${springBoot1Version}"

    }
}
project(':thymeleaf3') {
    dependencies {
        compile project(':stateless-csrf-filter-core')
        compile group: 'org.thymeleaf', name: 'thymeleaf', version: "${thymeLeaf3Version}"
        compile group: 'org.thymeleaf', name: 'thymeleaf-spring5', version: "${thymeLeaf3Version}"
        providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: "${servletAPIVersion}"

        testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "${springBoot1Version}"
        testCompile group: 'org.thymeleaf', name: 'thymeleaf-testing', version: "3.0.3.RELEASE"
    }
}
project(':thymeleaf2') {
    dependencies {
        compile project(':stateless-csrf-filter-core')
        compile group: 'org.thymeleaf', name: 'thymeleaf', version: "${thymeLeaf2Version}"
        compile group: 'org.thymeleaf', name: 'thymeleaf-spring4', version: "${thymeLeaf2Version}"
        providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: "${servletAPIVersion}"
        testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "${springBoot1Version}"
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'org.thymeleaf', name: 'thymeleaf-testing', version: "2.1.4.RELEASE"
    }
}
project(':example-thymeleaf3') {
    apply plugin:'application'
    mainClassName = "jp.classmethod.spring_stateless_csrf_filter.example_thymeleaf3.App"

    dependencies {
        compile project(":thymeleaf3")
        compile group: 'nz.net.ultraq.thymeleaf', name: 'thymeleaf-layout-dialect', version: '2.3.0'
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBoot2Version}"
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-parent', version: "${springBoot2Version}"
    }
}
project(':example-thymeleaf2') {
    apply plugin:'application'
    mainClassName = "jp.classmethod.spring_stateless_csrf_filter.example_thymeleaf2.App"

    dependencies {
        compile project(":thymeleaf2")
        compile group: 'nz.net.ultraq.thymeleaf', name: 'thymeleaf-layout-dialect', version: '1.4.0'
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBoot1Version}"
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-parent', version: "${springBoot1Version}"
    }
}
